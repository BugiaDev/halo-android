apply plugin: 'com.android.library'
apply plugin: 'grill'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    sourceSets {
        test.java.srcDirs += 'build/generated/source/r/debug'
    }

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode grill.CI.getCommitRevision()
        versionName "${rootProject.ext.haloVersion}"
    }
}

tasks.whenTaskAdded { task ->
    if (task.name.contains("lint")) {
        // Remove lint tasks to avoid linting the testing project that gets stuck on build time
        task.enabled = false
    }
}

android.variantFilter { variant ->
    if(!project.hasProperty("prepare${variant.name.capitalize()}Dependencies")) {
        task "prepare${variant.name.capitalize()}Dependencies"()
    }
}

dependencies {
    api "com.intellij:annotations:${rootProject.ext.intellijAnnotations}"
    api "junit:junit:${rootProject.ext.jUnitVersion}"
    api "org.robolectric:robolectric:${rootProject.ext.robolectricLibrary}"
    api "org.mockito:mockito-core:${rootProject.ext.mockitoLibrary}"
    api "org.assertj:assertj-core:${rootProject.ext.assertJVersion}"
    api 'com.google.code.findbugs:jsr305:3.0.2'
    api group: 'com.google.errorprone', name: 'error_prone_annotations', version: '2.0.2'
    api "com.squareup.okhttp3:mockwebserver:${rootProject.ext.mockWebServerVersion}"
}

/**
 * Distribution
 */
group = rootProject.ext.haloGroup
version = rootProject.ext.haloVersion
description = 'The HALO Testing provides the developer with some testing tools that may help him to test stuff that uses halo'

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
}

tasks.withType(Javadoc) {
    options.addStringOption('encoding', 'UTF-8')
}

task javadocJar(type: Jar, dependsOn: "documentationRelease") {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

grill {
    distribute {
        libName 'HALO Testing'
        libDescription 'This library provides the developer with some testing tools that may help him to test stuff that uses halo'
        libDevId "halo-mobgen"
        libDevName "MOBGEN"
        libDevEmail "halo@mobgen.com"
        libSiteUrl "https://halo.mobgen.com"
        libGitUrl "https://github.com/mobgen/halo-android/tree/master/sdk/halo-testing"
        bintrayRepoName "HALO"
        bintrayUser "${rootProject.ext.jcenterUser}"
        bintrayKey "${rootProject.ext.jcenterKey}"
        bintrayMavenCentralUser "${rootProject.ext.sonatypeUser}"
        bintrayMavenCentralPassword "${rootProject.ext.sonatypePassword}"
        bintrayArtifactJavadoc project.tasks.getByName("javadocJar")
        bintrayArtifactSources project.tasks.getByName("sourcesJar")
        bintrayMavenSync project.hasProperty("sync")
    }
    documentation {
        templateDir "/../docs/template"
    }
}