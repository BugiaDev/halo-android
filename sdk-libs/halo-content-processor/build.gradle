apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

//Known issue in jacoco: https://github.com/robolectric/robolectric/issues/2230
jacoco {
    toolVersion = "${rootProject.ext.jacocoVersion}"
}

dependencies{
    compile "com.squareup:javapoet:${rootProject.ext.javapoetVersion}"
    compile "com.google.auto.service:auto-service:${rootProject.ext.autoServiceVersion}"
    compile project(path: ':halo-content-annotations')
}

/**
 * Distribution
 */
group = "${rootProject.ext.haloGroup}"
version = "${rootProject.ext.haloVersion}"

task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// add javadoc/source jar tasks as artifacts
artifacts {
    archives sourcesJar, javadocJar
}

install {
    repositories.mavenInstaller {
        pom.project {
            packaging 'jar'
            groupId group
            artifactId 'HALO Content processor'

            name 'HALO Content processor'
            description 'Processor to generate compile time information that allows us to perform queries on sync'
            url "https://halo.mobgen.com"

            // Set your license
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }
            developers {
                developer {
                    id "halo-mobgen"
                    name "MOBGEN"
                    email "halo@mobgen.com"
                }
            }
            scm {
                connection "https://github.com/mobgen/halo-android/tree/master/sdk-libs/halo-content-processor"
                developerConnection "https://github.com/mobgen/halo-android/tree/master/sdk-libs/halo-content-processor"
                url "https://halo.mobgen.com"
            }
        }
    }
}

bintray {
    user = "${rootProject.ext.jcenterUser}"
    key = "${rootProject.ext.jcenterKey}"

    configurations = ['mavenJava']
    pkg {
        repo = 'maven'
        name = "HALO-Content-Processor"
        desc = 'Processor to generate compile time information that allows us to perform queries on sync'
        websiteUrl = 'https://halo.mobgen.com'
        vcsUrl = 'https://github.com/mobgen/halo-android/tree/master/sdk-libs/halo-content-processor'
        licenses = ["Apache-2.0"]
        publish = project.hasProperty("sync")
        publicDownloadNumbers = true
        version {
            desc = 'Processor to generate compile time information that allows us to perform queries on sync'
            gpg {
                sign = true
                passphrase = null
            }
            mavenCentralSync {
                sync = project.hasProperty("sync")
                user = "${rootProject.ext.sonatypeUser}"
                password = "${rootProject.ext.sonatypePassword}"
                close = project.hasProperty("sync") ? "1" : "0"
            }
        }
    }
}

task distribute (dependsOn: 'bintrayUpload') {

}
